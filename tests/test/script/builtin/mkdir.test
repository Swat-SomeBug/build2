# file      : tests/test/script/builtin/mkdir.test
# copyright : Copyright (c) 2014-2017 Code Synthesis Ltd
# license   : MIT; see accompanying LICENSE file

.include ../common.test

: dirs
:
$c <<EOI;
mkdir a b;
touch a/a b/b
EOI
$b

: parent
:
$c <<EOI;
mkdir -p a/b;
touch a/a a/b/b
EOI
$b

: exists
:
$c <'mkdir -p a a a/b a/b';
$b

: double-dash
:
: Make sure '-p' directory is created.
:
$c <<EOI;
mkdir -p -- -p;
touch -p/a
EOI
$b

: no-args
:
: Test passing no arguments.
:
$c <'mkdir 2>"mkdir: missing directory" == 1';
$b

: empty-path
:
: Test creation of empty directory path.
:
$c <<EOI;
mkdir '' 2>"mkdir: invalid path ''" == 1
EOI
$b

: already-exists
:
: Test creation of an existing directory.
:
: Note that there is an optional trailing blank line in the regex that matches
: the newline added by msvcrt as a part of the error description.
:
$c <<EOI;
mkdir a a 2>>~%EOE% == 1
%mkdir: unable to create directory '.+[/\\]test[/\\]mkdir[/\\]already-exists[/\\]test[/\\]1[/\\]a': .+%
%%?
EOE
EOI
$b

: not-exists
:
: Test creation of a directory with non-existent parent.
:
: Note that there is an optional trailing blank line in the regex that matches
: the newline added by msvcrt as a part of the error description.
:
$c <<EOI;
mkdir a/b 2>>~%EOE% == 1
%mkdir: unable to create directory '.+[/\\]test[/\\]mkdir[/\\]not-exists[/\\]test[/\\]1[/\\]a[/\\]b': .+%
%%?
EOE
EOI
$b
