# file      : unit-tests/test/script/parser/include.test
# copyright : Copyright (c) 2014-2017 Code Synthesis Ltd
# license   : MIT; see accompanying LICENSE file

: none
:
$* <<EOI
.include
.include --once
EOI

: empty
:
touch foo.test;
$* <<EOI
.include foo.test
.include --once foo.test
EOI

: one
:
cat <"cmd" >>>foo.test;
$* <<EOI >>EOO
.include foo.test
EOI
cmd
EOO

: multiple
:
cat <"cmd foo" >>>foo.test;
cat <"cmd bar" >>>bar.test;
$* <<EOI >>EOO
.include foo.test bar.test
EOI
cmd foo
cmd bar
EOO

: once
:
cat <"cmd" >>>foo.test;
$* <<EOI >>EOO
.include foo.test
x
.include --once foo.test
.include --once bar/../foo.test
y
.include ../once/foo.test
EOI
cmd
x
y
cmd
EOO

: group-id
:
cat <<EOI >>>foo.test;
{
  x = b
}
EOI
$* -s -i <<EOI >>EOO
x = a
.include foo.test
EOI
{
  { # 2-foo-1
  }
}
EOO

: test-id
:
cat <<EOI >>>foo.test;
cmd
EOI
$* -s -i <<EOI >>EOO
x = a
.include foo.test
EOI
{
  { # 2-foo-1
    cmd
  }
}
EOO

: invalid-path
:
$* <<EOI 2>>EOE != 0
.include ""
EOI
testscript:1:2: error: invalid testscript include path ''
EOE

: unable-open
:
$* <<EOI 2>>~/EOE/ != 0
.include foo.test
EOI
/testscript:1:2: error: unable to read testscript foo.test: .+/
EOE
