# file      : unit-tests/test/script/parser/redirect.test
# copyright : Copyright (c) 2014-2017 Code Synthesis Ltd
# license   : MIT; see accompanying LICENSE file

# @@ Add tests for redirects other than file and merge.
# @@ Does it make sense to split into separate files - one per redirect type?
#

: file
:
{
  : not-quote
  :
  $* <<EOI >>EOO
  cmd 0<<<a 1>>>b 2>>>&c
  EOI
  cmd <<<a >>>b 2>>>&c
  EOO

  : quote
  :
  $* <<EOI >>EOO
  cmd 0<<<"a f" 1>>>"b f" 2>>>&"c f"
  EOI
  cmd <<<'a f' >>>'b f' 2>>>&'c f'
  EOO

  : in
  :
  {
    : missed
    :
    $* <<EOI 2>>EOE !=0
    cmd <<<
    EOI
    testscript:1:8: error: missing stdin file
    EOE

    : empty
    :
    $* <<EOI 2>>EOE !=0
    cmd <<<""
    EOI
    testscript:1:8: error: empty stdin redirect path
    EOE
  }

  : out
  :
  {
    : missed
    :
    $* <<EOI 2>>EOE !=0
    cmd >>>
    EOI
    testscript:1:8: error: missing stdout file
    EOE

    : empty
    :
    $* <<EOI 2>>EOE !=0
    cmd >>>""
    EOI
    testscript:1:8: error: empty stdout redirect path
    EOE
  }

  : err
  :
  {
    : missed
    :
    $* <<EOI 2>>EOE !=0
    cmd 2>>>
    EOI
    testscript:1:9: error: missing stderr file
    EOE

    : empty
    :
    $* <<EOI 2>>EOE !=0
    cmd 2>>>""
    EOI
    testscript:1:9: error: empty stderr redirect path
    EOE
  }
}

: merge
{
  : out
  :
  {
    : err
    :
    $* <<EOI >>EOO
    cmd 1>&2
    EOI
    cmd >&2
    EOO

    : no-mutual
    :
    $* <<EOI >>EOO
    cmd 1>&2 2>&1 2>a
    EOI
    cmd >&2 2>a
    EOO

    : not-descriptor
    :
    $* <<EOI 2>>EOE != 0
    cmd 1>&a
    EOI
    testscript:1:8: error: stdout merge redirect file descriptor must be 2
    EOE

    : self
    :
    $* <<EOI 2>>EOE != 0
    cmd 1>&1
    EOI
    testscript:1:8: error: stdout merge redirect file descriptor must be 2
    EOE

    : missed
    :
    $* <<EOI 2>>EOE != 0
    cmd 1>&
    EOI
    testscript:1:8: error: missing stdout file descriptor
    EOE
  }

  : err
  {
    : out
    :
    $* <<EOI >>EOO
    cmd 2>&1
    EOI
    cmd 2>&1
    EOO

    : no-mutual
    :
    $* <<EOI >>EOO
    cmd 1>&2 2>&1 >a
    EOI
    cmd >a 2>&1
    EOO

    : not-descriptor
    :
    $* <<EOI 2>>EOE != 0
    cmd 2>&a
    EOI
    testscript:1:8: error: stderr merge redirect file descriptor must be 1
    EOE

    : self
    :
    $* <<EOI 2>>EOE != 0
    cmd 2>&2
    EOI
    testscript:1:8: error: stderr merge redirect file descriptor must be 1
    EOE

    : missed
    :
    $* <<EOI 2>>EOE != 0
    cmd 2>&
    EOI
    testscript:1:8: error: missing stderr file descriptor
    EOE
  }

  : mutual
  :
  $* <<EOI 2>>EOE != 0
  cmd 1>&2 2>&1
  EOI
  testscript:1:14: error: stdout and stderr redirected to each other
  EOE
}
